#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ОбновлениеПланировщика()
	
	КалендарьСпециалистов.Элементы.Очистить();
	
	НачалоПериода = НачалоМесяца(ТекущаяДатаСеанса());
	КонецПериода = КонецМесяца(ТекущаяДатаСеанса());
	
	КалендарьСпециалистов.ТекущиеПериодыОтображения.Добавить(НачалоПериода, КонецПериода);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_ОбслуживаниеКлиента.ВремяНачалаРаботПлан,
	|	ВКМ_ОбслуживаниеКлиента.ВремяОкончанияРаботПлан,
	|	ВКМ_ОбслуживаниеКлиента.Сотрудник,
	|	ВКМ_ОбслуживаниеКлиента.Ссылка,
	|	ВКМ_ОбслуживаниеКлиента.ДатаПроведенияРабот
	|ИЗ
	|	Документ.ВКМ_ОбслуживаниеКлиента КАК ВКМ_ОбслуживаниеКлиента
	|ГДЕ
	|	ВКМ_ОбслуживаниеКлиента.ДатаПроведенияРабот МЕЖДУ &ДатаНачала И &ДатаОкончания";
	Запрос.УстановитьПараметр("ДатаНачала", НачалоПериода);
	Запрос.УстановитьПараметр("ДатаОкончания", КонецПериода);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ДатаНачалаРабот = Выборка.ДатаПроведенияРабот + (Выборка.ВремяНачалаРаботПлан - Дата(1,1,1));
		ДатаОкончанияРабот = Выборка.ДатаПроведенияРабот + (Выборка.ВремяОкончанияРаботПлан - Дата(1,1,1));
		ЭлементПланировщика = КалендарьСпециалистов.Элементы.Добавить(ДатаНачалаРабот, ДатаОкончанияРабот);
		ЭлементПланировщика.Значение = Выборка.Ссылка;
		ЭлементПланировщика.Текст = Выборка.Сотрудник;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КалендарьСпециалистовПередСозданием(Элемент, Начало, Конец, ЗначенияИзмерений, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("ВремяНачалаРаботПлан", Начало);
	ЗначенияЗаполнения.Вставить("ВремяОкончанияРаботПлан", Конец);
	ЗначенияЗаполнения.Вставить("ДатаПроведенияРабот", Начало);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФОрму("Документ.ВКМ_ОбслуживаниеКлиента.Форма.ФормаДокумента", ПараметрыФормы);
	
	ОбновлениеПланировщика();
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Обновить(Команда)
	ОбновлениеПланировщика();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ВКМ_ОбслуживаниеКлиента" Тогда
		ОбновлениеПланировщика();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ПравоДоступа("Использование", Метаданные.Обработки.ВКМ_КалендарьСпециалистов) Тогда
		ОбновлениеПланировщика();
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти
